```@meta
CurrentModule = Antique
```

# Harmonic Oscillator

The harmonic oscillator is the most frequently used model in quantum physics.

## Definitions

```@docs; canonical=false
Antique.HarmonicOscillator
```

#### Potential
```@docs; canonical=false
Antique.V(::HarmonicOscillator, ::Any)
```

#### Eigenvalues
```@docs; canonical=false
Antique.E(::HarmonicOscillator)
```

#### Eigenfunctions
```@docs; canonical=false
Antique.ψ(::HarmonicOscillator, ::Any)
```

#### Hermite Polynomials
```@docs; canonical=false
Antique.H(::HarmonicOscillator, ::Any)
```

## Usage & Examples

[Install Antique.jl](@ref Install) for the first use and run `using Antique` before each use. The energy `E()`, wave function `ψ()` and potential `V()` will be exported. In this system, the model is generated by `HarmonicOscillator` and several parameters `k`, `m` and `ℏ` are set as optional arguments.

```@example HO
using Antique
HO = HarmonicOscillator(k=1.0, m=1.0, ℏ=1.0)
; #hide
```

Parameters:

```@repl HO
HO.k
HO.m
HO.ℏ
```

Eigenvalues:

```@repl HO
E(HO, n=0)
E(HO, n=1)
```

Potential energy curve:

```@example HO
using CairoMakie

f = Figure()
ax = Axis(f[1,1], xlabel=L"$x$", ylabel=L"$V(x)$")
lines!(ax, -5..5, x -> V(HO, x))
f
```

Wave functions:

```@example HO
using CairoMakie

# setting
f = Figure()
ax = Axis(f[1,1], xlabel=L"$x$", ylabel=L"$\psi(x)$")

# plot
w0 = lines!(ax, -5..5, x -> ψ(HO, x, n=0))
w1 = lines!(ax, -5..5, x -> ψ(HO, x, n=1))
w2 = lines!(ax, -5..5, x -> ψ(HO, x, n=2))
w3 = lines!(ax, -5..5, x -> ψ(HO, x, n=3))
w4 = lines!(ax, -5..5, x -> ψ(HO, x, n=4))

# legend
axislegend(ax, [w0, w1, w2, w3, w4], [L"n=0", L"n=1", L"n=2", L"n=3", L"n=4"], position=:lb)

f
```

Potential energy curve, Energy levels, Wave functions:

```@example HO
using CairoMakie

# settings
f = Figure()
ax = Axis(f[1,1], xlabel=L"$x$", ylabel=L"$V(x),~E_n,~\psi_n(x) \times 5 + E_n$", aspect=1, limits=(-5,5,0,5.2))
# hidespines!(ax)
# hidedecorations!(ax)

for n in 0:4
  # classical turning point
  xE = sqrt(2*HO.k*E(HO, n=n))
  # energy
  lines!(ax, [-xE,xE], fill(E(HO,n=n),2), color=:black, linewidth=2)
  hlines!(ax, E(HO, n=n), color=:black, linewidth=1, linestyle=:dash)
  # wave function
  lines!(ax, -5..5, x -> E(HO,n=n) + 0.5*ψ(HO,x,n=n), linewidth=2)
end

#potential
lines!(ax, -5..5, x -> V(HO, x), color=:black, linewidth=2)

f
save("assets/fig/HarmonicOscillator.png", f) # hide
; # hide
```

![](assets/fig/HarmonicOscillator.png)

## Testing

Unit testing and Integration testing were done using a computer algebra system ([Symbolics.jl](https://symbolics.juliasymbolics.org/stable/)) and numerical integration ([QuadGK.jl](https://juliamath.github.io/QuadGK.jl/stable/)). The test script is [here](https://github.com/ohno/Antique.jl/blob/main/test/HarmonicOscillator.jl).

```@eval
using Markdown
using Antique
Markdown.parse(Antique.load("../../test/result/HarmonicOscillator.md"))
```
