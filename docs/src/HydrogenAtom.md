```@meta
CurrentModule = Antique
```

# Hydrogen Atom

The hydrogen atom is the simplest Coulomb 2-body system.

## Definitions

```@docs; canonical=false
Antique.HydrogenAtom
```

#### Potential
```@docs; canonical=false
Antique.V(::HydrogenAtom, ::Any)
```

#### Eigenvalues
```@docs; canonical=false
Antique.E(::HydrogenAtom)
```

#### Eigenfunctions
```@docs; canonical=false
Antique.ψ(::HydrogenAtom, ::Any, ::Any, ::Any)
```

#### Radial Functions
```@docs; canonical=false
Antique.R(::HydrogenAtom, ::Any)
```

#### Associated Laguerre Polynomials
```@docs; canonical=false
Antique.L(::HydrogenAtom, ::Any)
```

#### Spherical Harmonics
```@docs; canonical=false
Antique.Y(::HydrogenAtom, ::Any, ::Any)
```

#### Associated Legendre Polynomials
```@docs; canonical=false
Antique.P(::HydrogenAtom, ::Any)
```

## Usage & Examples

[Install Antique.jl](@ref Install) for the first use and run `using Antique` before each use. The energy `E()`, wave function `ψ()` and potential `V()` will be exported. In this system, the model is generated by `HydrogenAtom` and several parameters `Z`, `Eₕ`, `mₑ`, `a₀` and `ℏ` are set as optional arguments.

```@example HA
using Antique
H = HydrogenAtom(Z=1, Eₕ=1.0, a₀=1.0, mₑ=1.0, ℏ=1.0)
; #hide
```

Parameters:

```@repl HA
H.Z
H.Eₕ
H.mₑ
H.a₀
H.ℏ
```

Eigenvalues:

```@repl HA
E(H, n=1)
E(H, n=2)
```

Wavelength ($n=2\rightarrow1$, the first line of the Lyman series):

```@example HA
Eₕ2nm⁻¹ = 2.1947463136320e-2 # https://physics.nist.gov/cgi-bin/cuu/CCValue?hrminv
println("ΔE = ", E(H,n=2) - E(H,n=1), " Eₕ")
println("λ  = ", ((E(H,n=2)-E(H,n=1))*Eₕ2nm⁻¹)^-1, " nm")
```

Hyperfine Splitting:

```@example HA
# E. Tiesinga, et al., Rev. Mod. Phys. 93, 025010 (2021) https://doi.org/10.1103/RevModPhys.93.025010
e  = 1.602176634e-19    # C      https://physics.nist.gov/cgi-bin/cuu/Value?e
h  = 6.62607015e-34     # J Hz-1 https://physics.nist.gov/cgi-bin/cuu/Value?h
c  = 299792458          # m s-1  https://physics.nist.gov/cgi-bin/cuu/Value?c
a0 = 5.29177210903e-11  # m      https://physics.nist.gov/cgi-bin/cuu/Value?bohrrada0
μ0 = 1.25663706212e-6   # N A-2  https://physics.nist.gov/cgi-bin/cuu/Value?mu0
μB = 9.2740100783e-24   # J T-1  https://physics.nist.gov/cgi-bin/cuu/Value?mub
μN = 5.0507837461e-27   # J T-1  https://physics.nist.gov/cgi-bin/cuu/Value?mun
ge = 2.00231930436256   #        https://physics.nist.gov/cgi-bin/cuu/Value?gem
gp = 5.5856946893       #        https://physics.nist.gov/cgi-bin/cuu/Value?gp

# D. J. Griffiths, Am. J. Phys. 50, 698 (1982) https://doi.org/10.1119/1.12733
δ = abs(ψ(H,0,0,0))^2
ΔE = 2 / 3 * μ0 * μN * μB * gp * ge * δ * a0^(-3)
println("1/π    = ", 1/π)
println("<δ(r)> = ", δ, " a₀⁻³")
println("<δ(r)> = ", δ * a0^(-3), " m⁻³")
println("ΔE = ", ΔE, " J")
println("ν = ΔE/h = ", ΔE / h * 1e-6, " MHz")
println("λ = hc/ΔE = ", h*c/ΔE*100, " cm")
```

Potential energy curve:

```@example HA
using CairoMakie

f = Figure()
ax = Axis(f[1,1], xlabel=L"$r~/~a_0$", ylabel=L"$V(r)~/~E_\mathrm{h}$",  limits=(0.0,15.0,-2.0,0.2))
lines!(ax, 0.1:0.01:20, r -> V(H, r))
f
```

1S wave function:

```@example HA
import Antique
H = Antique.HydrogenAtom(Z=1, mₑ=1.0, a₀=1.0, Eₕ=1.0, ℏ=1.0)
@show Antique.E(H)

using CairoMakie

fig = Figure(
    size = (420,300),
    fontsize = 11,
    backgroundcolor = :transparent
)

ax = Axis(
    fig[1,1],
    xlabel = L"$r / a_0$",
    ylabel = L"$\psi(r) / a_0^{-3/2}$",
    ylabelsize = 16.5,
    xlabelsize = 16.5,
)

lines!(ax, 0..5, r -> exp(-r)/sqrt(π), label="exp(-r)/sqrt(π)")
lines!(ax, 0..1, r -> (1-r)/sqrt(π), label="(1-r)/sqrt(π)")
lines!(ax, 0..5, r -> abs(Antique.ψ(H,r,0,0)), linestyle=:dash, color=:black, label="Antique.jl")

axislegend(ax, position=:rt, framevisible=false)

fig
```

Radial functions:

```@example HA
using CairoMakie
using LaTeXStrings

# setting
f = Figure()
ax = Axis(f[1,1], xlabel=L"$r~/~a_0$", ylabel=L"$r^2|R_{nl}(r)|^2~/~a_0^{-1}$", limits=(0,20,0,0.58))

# plot
ws = []
ls = []
for n in 1:3
  for l in 0:n-1
    w = lines!(
        ax,
        0..20,
        r -> r^2 * Antique.R(H,r,n=n,l=l)^2,
        linewidth = 2,
        linestyle = [:solid,:dash,:dot,:dashdot,:dashdotdot][l+1],
        color = n,
        colormap = :tab10,
        colorrange = (1,10)
    )
    push!(ws, w)
    push!(ls, latexstring("n=$n, l=$l"))
  end
end

# legend
axislegend(ax, ws, ls, position=:rt)

f
```

## Testing

Unit testing and Integration testing were done using a computer algebra system ([Symbolics.jl](https://symbolics.juliasymbolics.org/stable/)) and numerical integration ([QuadGK.jl](https://juliamath.github.io/QuadGK.jl/stable/)). The test script is [here](https://github.com/ohno/Antique.jl/blob/main/test/HydrogenAtom.jl).

```@eval
using Markdown
using Antique
Markdown.parse(Antique.load("../../test/result/HydrogenAtom.md"))
```
